10 ON ERROR GOTO 5000
20 METHOD$ = ENVIRON$("REQUEST_METHOD")
30 IF METHOD$ <> "DELETE" GOTO 4050

60 QS$ = ENVIRON$("QUERY_STRING")
61 IDX = INSTR(QS$, "id=")
62 IF IDX <= 0 GOTO 4000
63 IDX = IDX + 3
64 ID = VAL(MID$(QS$, IDX, LEN(QS$) - IDX + 1))
65 IF ID <= 0 GOTO 4000

80 NAME "TODO.DAT" AS "TODO.OLD"
90 OPEN "TODO.OLD" FOR INPUT AS #1
100 OPEN "TODO.DAT" FOR OUTPUT AS #2
110 WHILE NOT EOF(1)
120 LINE INPUT #1, A$
130 ID = ID - 1
140 IF ID <> 0 THEN PRINT #2, A$
150 WEND
160 CLOSE #1
170 CLOSE #2
180 KILL "TODO.OLD"
190 IF ID > 0 GOTO 4040

200 PRINT "Content-Type: text/plain"
210 PRINT "X-Powered-By: GW-BASIC"
220 PRINT ""
230 GOTO 9999

4000 PRINT "Status: 400 Bad request"
4001 PRINT ""
4002 PRINT QS$
4003 GOTO 9999

4040 PRINT "Status: 404 Not found"
4041 PRINT ""
4042 GOTO 9999

4050 PRINT "Status: 405 Method not allowed, send POST"
4051 PRINT ""
4052 GOTO 9999

4150 PRINT "Status: 415 Unsupported media type, I only speak text/plain"
4151 PRINT ""
4152 GOTO 9999

5000 PRINT "Status: 500 Internal Server Error"
5001 PRINT ""
5002 ON ERROR GOTO 0

9999 SYSTEM
